// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx
    Mux16(a=x, b=false, sel=zx, out=outzx);
    // nx
    Not16(in=outzx, out=notzx);
    Mux16(a=outzx, b=notzx, sel=nx, out=outnx);
    // zy
    Mux16(a=y, b=false, sel=zy, out=outzy);
    // ny
    Not16(in=outzy, out=notzy);
    Mux16(a=outzy, b=notzy, sel=ny, out=outny);
    // f
    And16(a=outnx, b=outny, out=andxy);
    Add16(a=outnx, b=outny, out=addxy);
    Mux16(a=andxy, b=addxy, sel=f, out=outf);
    // no
    Not16(in=outf, out=notoutf);
    Mux16(a=outf, b=notoutf, sel=no, out=out, out[15]=ngcheck, out[0..7]=zrcheck1, out[8..15]=zrcheck2);
    // zr
    Or8Way(in=zrcheck1, out=or1);
    Or8Way(in=zrcheck2, out=or2);
    Or(a=or1, b=or2, out=notzr);
    Not(in=notzr, out=zr);
    // ng
    And(a=ngcheck, b=true, out=ng);
}